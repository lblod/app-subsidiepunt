PREFIX subsidie: <http://data.vlaanderen.be/ns/subsidie#>
PREFIX dct: <http://purl.org/dc/terms/>
PREFIX adms: <http://www.w3.org/ns/adms#>
PREFIX common: <http://www.w3.org/2007/uwa/context/common.owl#>

# UPDATE LEKP 1.0 SUBSIDIE APPLICATIONS THAT HAVE THEIR LAST STEP STATUS SET TO 'SENT'
# Because of a previous bug, even if orgs submitted the subsidy, it would not be set to the 'SENT' status

DELETE {
  GRAPH ?g {
    ?consumption adms:status <http://lblod.data.gift/concepts/c849ca98-455d-4f31-9e95-a3d9d06e4497> . # status 'ACTIVE'
  }
}
INSERT {
  GRAPH ?g {
    ?consumption adms:status <http://lblod.data.gift/concepts/2ea29fbf-6d46-4f08-9343-879282a9f484> . # status 'SENT'
  }
} WHERE {
  GRAPH ?g {
    ?consumption a subsidie:SubsidiemaatregelConsumptie .
    ?consumption adms:status <http://lblod.data.gift/concepts/c849ca98-455d-4f31-9e95-a3d9d06e4497> .
    ?consumption dct:source ?applicationform .
    ?applicationform dct:isPartOf <http://lblod.data.info/id/subsidie-application-flow-steps/10b2a1d8-f3ca-44d7-b987-6eeb7b844107> .
    ?applicationform adms:status <http://lblod.data.gift/concepts/9bd8d86d-bb10-4456-a84e-91e9507c374c> .
  }
  FILTER(REGEX(STR(?g), "^http://mu.semte.ch/graphs/organizations/.+/SubsidiepuntGebruiker$"))
}

;

# Fixes original bug

INSERT DATA {
  GRAPH <http://mu.semte.ch/graphs/public> {
     <http://data.lblod.info/id/subsidy-procedural-steps/b5fbbf47-32b7-4f3d-bf5c-2589fcb24960> subsidie:Subsidieprocedurestap.type <http://lblod.data.gift/concepts/ee855aae-eb10-44aa-ae01-38c4f6cca0f6>
    }
}

;

# Set active application flow step for all LEKP 1.0 consumptions for consumptions that do not have status 'SENT'
INSERT {
  GRAPH ?g {
    ?consumption common:active <http://data.lblod.info/id/subsidie-application-flow-steps/10b2a1d8-f3ca-44d7-b987-6eeb7b844107>
  }
} WHERE {
  GRAPH ?h {
    ?consumption a subsidie:SubsidiemaatregelConsumptie .
    ?consumption adms:status <http://lblod.data.gift/concepts/c849ca98-455d-4f31-9e95-a3d9d06e4497>. # status 'ACTIVE'
    ?consumption dct:source ?applicationform .
    ?applicationform dct:isPartOf <http://lblod.data.info/id/subsidie-application-flow-steps/10b2a1d8-f3ca-44d7-b987-6eeb7b844107> .

    BIND(?h AS ?g)
    FILTER(REGEX(STR(?h), "^http://mu.semte.ch/graphs/organizations/.+/SubsidiepuntGebruiker$"))
  }
}
