PREFIX mobiliteit: <https://data.vlaanderen.be/ns/mobiliteit#>
PREFIX lblodSubsidie: <http://lblod.data.gift/vocabularies/subsidie/>
PREFIX dct: <http://purl.org/dc/terms/>
PREFIX subsidie: <http://data.vlaanderen.be/ns/subsidie#>
PREFIX mu: <http://mu.semte.ch/vocabularies/core/>
PREFIX qb: <http://purl.org/linked-data/cube#>
PREFIX cpsv: <http://purl.org/vocab/cpsv#>
PREFIX xkos: <http://rdf-vocabulary.ddialliance.org/xkos#>
PREFIX adms: <http://www.w3.org/ns/adms#>
PREFIX common: <http://www.w3.org/2007/uwa/context/common.owl#>

INSERT DATA {
  GRAPH <http://mu.semte.ch/graphs/public> {
    # Create new subsidy-procedural-step
    <http://data.lblod.info/id/subsidy-procedural-steps/f3802658-d8c7-4de1-b5eb-4820a323d4ab> a subsidie:Subsidieprocedurestap;
      mu:uuid "f3802658-d8c7-4de1-b5eb-4820a323d4ab";
      dct:description """Aanvraag (fase 2)""";
      subsidie:Subsidieprocedurestap.type <http://lblod.data.gift/concepts/ee855aae-eb10-44aa-ae01-38c4f6cca0f6>;
      mobiliteit:periode <http://data.lblod.info/id/periodes/5c52fedd-290c-4c6e-baf9-781e763d31fa>.
    # Create new subsidie-application-flow-step
    <http://data.lblod.info/id/subsidie-application-flow-steps/2981e84e-b631-4d37-b6bb-4799df654df3> a lblodSubsidie:ApplicationStep;
      mu:uuid "2981e84e-b631-4d37-b6bb-4799df654df3";
      qb:order 1;
      xkos:previous <http://data.lblod.info/id/subsidie-application-flow-steps/fee3b99d-6436-4f0c-ad7f-94afa8adae51>;
      dct:references <http://data.lblod.info/id/subsidy-procedural-steps/f3802658-d8c7-4de1-b5eb-4820a323d4ab>;
      dct:isPartOf <http://data.lblod.info/id/subsidie-application-flows/2385e555-d6b9-476c-8884-06594db5093c>;
      dct:source <config://forms/urban-renewal/aanvraag-fase-2/versions/20250805122727-oproep-2025/form.ttl>.

    # New period for step aanvraag (fase 2)
    <http://data.lblod.info/id/periodes/5c52fedd-290c-4c6e-baf9-781e763d31fa> a <http://data.europa.eu/m8g/PeriodOfTime>;
      mu:uuid "5c52fedd-290c-4c6e-baf9-781e763d31fa";
      # Time is set to 10 April 23:00 because UTC+1 this becomes 11 April 00:00
      <http://data.europa.eu/m8g/startTime> "2025-04-10T23:00:00Z"^^<http://www.w3.org/2001/XMLSchema#dateTime>;
      # Time is set to 24 September 22:59:00 because UTC+2 in September and it becomes 23:59.
      <http://data.europa.eu/m8g/endTime> "2025-09-24T22:59:00Z"^^<http://www.w3.org/2001/XMLSchema#dateTime>.

    # Add link from subsidy-measure-offer-series to subsidy-procedural-step
    <http://data.lblod.info/id/subsidy-measure-offer-series/18c21437-78ae-45e7-92f3-f8d3e62fa703> lblodSubsidie:heeftSubsidieprocedurestap <http://data.lblod.info/id/subsidy-procedural-steps/f3802658-d8c7-4de1-b5eb-4820a323d4ab>.

    <http://data.lblod.info/id/subsidie-application-flow-steps/fee3b99d-6436-4f0c-ad7f-94afa8adae51> xkos:next <http://data.lblod.info/id/subsidie-application-flow-steps/2981e84e-b631-4d37-b6bb-4799df654df3>.
  }
}
;

DELETE {
  GRAPH ?targetGraph {
    ?consumption adms:status ?oldStatus.
  }
}
INSERT {
  GRAPH ?targetGraph {
    ?consumption adms:status <http://lblod.data.gift/concepts/c849ca98-455d-4f31-9e95-a3d9d06e4497> ;
                 common:active <http://data.lblod.info/id/subsidie-application-flow-steps/2981e84e-b631-4d37-b6bb-4799df654df3> .
  }
}
WHERE {
  VALUES ?bestuureenheid {
    <http://data.lblod.info/id/bestuurseenheden/353234a365664e581db5c2f7cc07add2534b47b8e1ab87c821fc6e6365e6bef5> # Gent
    <http://data.lblod.info/id/bestuurseenheden/70b79f6678036bf05e970aa3885d1779f143d4eab63ecf339ea6263e7e76ad1d> # Herentals
    <http://data.lblod.info/id/bestuurseenheden/8fd5ea9aeb61c45ec79986650bee55142b2f8a599d5d611dd578114216a58430> # Kortrijk
    <http://data.lblod.info/id/bestuurseenheden/c648ea5d12626ee3364a02debb223908a71e68f53d69a7a7136585b58a083e77> # Leuven
    <http://data.lblod.info/id/bestuurseenheden/55155a78bd145df7bfa3caaa17ba491fb4dd238f4f4d2c5485bff1be96ca3036> # Mol
    <http://data.lblod.info/id/bestuurseenheden/764a0c6bbc866153ae70cf75d745b9477fa010567246cfe6683b7bb8aec541b4> # Oostende
    <http://data.lblod.info/id/bestuurseenheden/3e58880a542424b73f85c9ffba8837b0da40d8c43e936c92603cde2015f5cdae> # Roeselare
  }

  ?bestuureenheid mu:uuid ?uuid.
  BIND(IRI(CONCAT("http://mu.semte.ch/graphs/organizations/", STR(?uuid), "/SubsidiepuntGebruiker")) AS ?targetGraph)

  GRAPH ?targetGraph {
    ?consumption cpsv:follows <http://data.lblod.info/id/subsidie-application-flows/2385e555-d6b9-476c-8884-06594db5093c> ;
                 adms:status ?oldStatus.
  }
}
