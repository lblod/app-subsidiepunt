PREFIX cleanup: <http://mu.semte.ch/vocabularies/ext/cleanup/>
PREFIX mu:      <http://mu.semte.ch/vocabularies/core/>
PREFIX dcterms: <http://purl.org/dc/terms/>

INSERT DATA {
  GRAPH <http://mu.semte.ch/graphs/public> {
    <http://data.lblod.info/job/id/7dafe8eb-de89-49c4-9bac-bcaf0222fa3c> a cleanup:Job;
      mu:uuid "7dafe8eb-de89-49c4-9bac-bcaf0222fa3c";
      dcterms:title "Compensating missing rdf:Type on some formNodes in some subsidy forms";
      cleanup:randomQuery """
        INSERT {
          GRAPH ?g{
            ?organization <http://www.w3.org/2004/02/skos/core#inScheme> <http://data.lblod.info/id/conceptscheme/EconomischeActoren>
          }
        } WHERE {
          GRAPH ?g {
            ?organization a <http://www.w3.org/ns/org#Organization>.
            
            # We dont want orgs like erediensten in our list at the moment.
            FILTER NOT EXISTS {
              ?organization a ?otherType.
              FILTER (?otherType NOT IN (<http://www.w3.org/ns/org#Organization>))
            }

            # Ensure the organization does not already have the scheme attached
            FILTER NOT EXISTS {
              ?organization <http://www.w3.org/2004/02/skos/core#inScheme> <http://data.lblod.info/id/conceptscheme/EconomischeActoren>
            }
          }
        }
      """;
      cleanup:cronPattern "0 * * * *". # Runs hourly
    }
  }
