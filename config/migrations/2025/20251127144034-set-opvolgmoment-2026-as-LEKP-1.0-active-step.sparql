PREFIX cpsv: <http://purl.org/vocab/cpsv#>
PREFIX common: <http://www.w3.org/2007/uwa/context/common.owl#>
PREFIX transactie: <http://data.vlaanderen.be/ns/transactie#>
PREFIX adms: <http://www.w3.org/ns/adms#>

# Add active step to opvolgmoment-2025 for LEKP 1.0 subsidy
DELETE {
  GRAPH ?g {
    # Clear any active expired opvolgmoment steps
    ?consumptie common:active ?oldStep .
  }
}
INSERT {
  GRAPH ?g {
    ?consumptie common:active <http://lblod.data.info/id/subsidie-application-flow-steps/9aa066f6-76fc-476c-a10f-6fb909b19248> # Opvolgmoment 2026
  }
} WHERE {
  GRAPH ?g {
    ?consumptie adms:status <http://lblod.data.gift/concepts/c849ca98-455d-4f31-9e95-a3d9d06e4497> . #Active
    ?consumptie transactie:isInstantieVan <http://lblod.data.info/id/subsidy-measure-offers/64d40351-8128-464f-990f-41066154583e> ; # LEKP 1.0
                cpsv:follows <http://lblod.data.info/id/subsidie-application-flows/83353c51-0d65-44bb-b3a7-6b8701b395c9> .

    VALUES ?oldStep {
      <http://lblod.data.info/id/subsidie-application-flow-steps/6977011a-a29c-11eb-b62a-7fee2a9561b8> # opvolgmoment
      <http://lblod.data.info/id/subsidie-application-flow-steps/3514ae35-e18e-48b5-8647-80c06d926c78> # opvolgmoment 2024
      <http://lblod.data.info/id/subsidie-application-flow-steps/10b2a1d8-f3ca-44d7-b987-6eeb7b844107> # opvolgmoment 2025
    }
  }
}

;

# Re-open the subsidies that were fully submitted and had the status of sent
DELETE {
  GRAPH ?g {
    ?consumptie
      adms:status <http://lblod.data.gift/concepts/2ea29fbf-6d46-4f08-9343-879282a9f484>. # sent
  }
}
INSERT {
  GRAPH ?g {
    ?consumptie
      adms:status <http://lblod.data.gift/concepts/c849ca98-455d-4f31-9e95-a3d9d06e4497>. # active

    ?consumptie common:active <http://lblod.data.info/id/subsidie-application-flow-steps/9aa066f6-76fc-476c-a10f-6fb909b19248>. # Opvolgmoment 2026
  }
}
WHERE {
  GRAPH ?g {
    ?consumptie adms:status <http://lblod.data.gift/concepts/2ea29fbf-6d46-4f08-9343-879282a9f484> . # sent
    ?consumptie transactie:isInstantieVan <http://lblod.data.info/id/subsidy-measure-offers/64d40351-8128-464f-990f-41066154583e> ; # LEKP 1.0
                cpsv:follows <http://lblod.data.info/id/subsidie-application-flows/83353c51-0d65-44bb-b3a7-6b8701b395c9> .
  }
}
